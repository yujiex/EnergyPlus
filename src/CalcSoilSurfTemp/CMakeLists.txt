project(CalcSoilSurfTemp Fortran)

cmake_minimum_required(VERSION 3.5.1)

cmake_policy(SET CMP0012 NEW) # if() recognizes boolean constants

set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/../../Products")
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/../../Products")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/../../Products")

set(SRC EPWRead.f90 SoilSurfTemp.f90)

set(STATIC_EXE FALSE)

add_library(CalcSoilSurfTempOptions INTERFACE)

if(APPLE)
  if(CMAKE_Fortran_COMPILER MATCHES "ifort")
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -fpp)
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -static-intel)
  else()
    if(NOT "Ninja" STREQUAL ${CMAKE_GENERATOR})
      target_compile_options(CalcSoilSurfTempOptions INTERFACE -cpp)
    endif()
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -ffree-line-length-200)
  endif()
elseif(UNIX)
  set(CMAKE_SKIP_RPATH TRUE)
  if(CMAKE_Fortran_COMPILER MATCHES "ifort")
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -fpp)
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -static-intel)
  else()
    if(NOT "Ninja" STREQUAL ${CMAKE_GENERATOR})
      target_compile_options(CalcSoilSurfTempOptions INTERFACE -cpp)
    endif()
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -static)
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -ffree-line-length-200)
    set(STATIC_EXE TRUE)
  endif()
else() # Windows
  set(STATIC_EXE TRUE)
  if(CMAKE_Fortran_COMPILER MATCHES "ifort")
    # Set release flags to be empty
    set(CMAKE_Fortran_FLAGS_RELEASE "")
    target_compile_options(CalcSoilSurfTempOptions INTERFACE /libs:static)
    target_compile_options(CalcSoilSurfTempOptions INTERFACE /fpp)
  else()
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -static)
    if(NOT "Ninja" STREQUAL ${CMAKE_GENERATOR})
      target_compile_options(CalcSoilSurfTempOptions INTERFACE -cpp)
    endif()
    target_compile_options(CalcSoilSurfTempOptions INTERFACE -ffree-line-length-200)
  endif()
endif()

set(SKIP_BUILD_RPATH TRUE)
add_executable(CalcSoilSurfTemp ${SRC})
target_link_libraries(CalcSoilSurfTemp PRIVATE CalcSoilSurfTempOptions)
set_target_properties(CalcSoilSurfTemp PROPERTIES FOLDER Auxiliary)

install(TARGETS CalcSoilSurfTemp DESTINATION PreProcess/CalcSoilSurfTemp)
if(NOT ${STATIC_EXE})
  include("${CMAKE_CURRENT_SOURCE_DIR}/../../cmake/ProjectMacros.cmake")
  install_target_prereqs(CalcSoilSurfTemp PreProcess/CalcSoilSurfTemp)
endif()
